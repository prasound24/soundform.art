<!doctype html>
<html>

<head>
  <title>The Art of Sound Forms</title>
  <link rel="icon" type="image/png" href="/img/favicon.png" />
  <link rel="stylesheet" href="/common.css" />
  <link rel="stylesheet" href="3dgs.css" />
</head>

<body>
  <h1 style="display:none">
    <a href="/"><img src="/img/favicon.png"></a>
    <a href="/prints">Prints</a>

    <span></span>
    <a href="/about">About</a>
  </h1>

  <div class="wave_spanner" style="display:none">
    <img src="/img/formula_div.png">
    <img src="/img/formula.png">
  </div>

  <canvas id="webgl"></canvas>
  <code id="codemirror" class="debug"></code>

  <div id="controls">
    <button id="pause" class="debug"><img src="/img/ico/stop.png"></button>
    <button id="show_code" class="debug"><img src="/img/ico/menu.png"></button>
    <button id="audio" title="Upload audio" class="debug"><img src="/img/ico/mic.png"></button>
    <button id="start_recording" class="debug"><img src="/img/ico/record.png"></button>
    <button id="stop_recording" class="debug"><img src="/img/ico/stop.png"></button>
  </div>

  <script id="spark-frag-glsl" type="x-shader/x-fragment">
    const float DENS = 15.0;

    float density(vec3 r) {
        r *= maxStdDev*sqrt(0.5);
        return DENS*vRgba.a*exp(-dot(r,r));
        //return DENS*vRgba.a*smoothstep(1., 0., dot(r,r));
    }

    float erfc(float x) {
        return sign(x)*sqrt(1. - exp2(-1.787776*x*x));
    }

    vec3 raymarchFog(vec3 ro, vec3 rd, vec3 pos) {
        float SR = maxStdDev*sqrt(0.5);
        vec3 lum = vec3(1);
        vec3 rs = pos - ro;
        float b = dot(rs, rd);
        float h = dot(rs, rs) - b*b;
        float sum = (1. + erfc(b*SR))/SR;
        sum *= sqrt(PI)*0.5;
        sum *= DENS*vRgba.a*exp(-SR*SR*h);
        lum *= exp(-sum);
        return lum;
    }

    vec2 raySphere(vec3 ro, vec3 rd, float r) {
        float b = dot(ro, rd);
        float h = b*b + r*r - dot(ro, ro);
        return max(vec2(0), -b - sqrt(max(0., h))*vec2(1,-1));
    }

    void main() {
      if (length(vSplatUv) > maxStdDev) discard;
      fragColor = vRgba;
      //fragColor.rgb *= fragColor.a;
      
      //float d = 0.1*pow(maxStdDev/length(vSplatUv), 1.5);
      float d = exp(-0.5*dot(vSplatUv,vSplatUv));
      //float d = smoothstep(1., 0., length(vSplatUv/maxStdDev));
      //float d = cos(length(vSplatUv/maxStdDev)*PI)*0.5 + 0.5;
      fragColor *= d;
      //fragColor.rgb *= exp(vNdcOffset.y/vNdcScales.y);
      return;

      vec3 c = (vNdc - vec3(vNdcOffset,0))/vec3(vNdcScales,1); // sphere center
      vec3 ro = vec3(vNdc.xy/vNdcScales,0);
      vec3 rd = vec3(0,0,1);
      vec3 sun = normalize(vec3(1,1,1)); // sunlight dir

      vec4 sum = vec4(0);
      vec2 t0 = raySphere(ro - c, rd, 1.);
      float ds = (t0.y - t0.x)*0.15;

      for (float t = t0.x + ds*0.5; t < t0.y; t += ds) {
        vec3 lum = raymarchFog(ro + rd*t, sun, c); // 0..1
        float w = ds*density(ro + rd*t - c);
        sum.rgb += w*exp(-sum.w)*lum;
        sum.w += w;
      }
      
      fragColor.rgb = 1.5*vRgba.rgb*sum.rgb;
      fragColor.a = 1. - exp(-sum.w);
    }
  </script>

  <script id="vert-glsl" type="x-shader/x-vertex">
    out vec2 vUv;

    void main() {
      vUv = uv;
      gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
    }
  </script>

  <script id="tonemapping-glsl" type="x-shader/x-fragment">
    uniform sampler2D tDiffuse;
    in vec2 vUv;

    void main() {
      vec4 o = texture(tDiffuse, vUv);
      o *= 0.1;
      //o = tanh(o);
      o = pow(o, vec4(1)/2.2);
      gl_FragColor = o;
    }
  </script>

  <script id="accumulator-glsl" type="x-shader/x-fragment">
    in vec2 vUv;

    uniform float iNumFrames;
    uniform sampler2D tDiffuse;
    uniform sampler2D tAccumulator;

    void main() {
      vec4 d = texture(tDiffuse, vUv);
      vec4 o = texture(tAccumulator, vUv);
      o.a = min(o.a, iNumFrames);
      o.rgb = mix(o, d, 1./(o.a + 1.)).rgb;
      o.a += 1.;
      gl_FragColor = o;
    }
  </script>

  <script id="sunrays-glsl" type="x-shader/x-fragment">
    uniform sampler2D tDiffuse;
    uniform  float iTime;
    in vec2 vUv;
    
    float hash( vec2 p ) {
      return fract(sin(dot(p, vec2(41, 289)))*45758.5453);
    }
 
    vec4 radialBlur(sampler2D img, vec2 uv) {
        float SAMPLES = 24.;
        float decay = 0.97; 
        float density = 0.5; 
        float weight = 0.1; 
        
        vec2 tuv =  uv - .5;
        vec2 dTuv = tuv*density/SAMPLES;
        vec4 col = texture(img, uv);
        uv += dTuv*(hash(uv + fract(iTime))*2. - 1.);
        
        for(float i = 0.; i < SAMPLES; i++){
            uv -= dTuv;
            vec4 src = texture(img, uv);
            col += weight * pow(src, vec4(8));
            weight *= decay;
        }
        
        col *= 1. - dot(tuv, tuv)*.75;
        return sqrt(smoothstep(0., 1., col));
    }

    void main() {
      gl_FragColor = radialBlur(tDiffuse, vUv);
    }
  </script>

  <script id="vignette-glsl" type="x-shader/x-fragment">
    in vec2 vUv;
    uniform sampler2D tDiffuse;
    uniform sampler2D tImageLogo;
    uniform sampler2D tSignature;

    float vignette() {
        vec2 uv = vUv;
        uv.xy *= 1. - uv.yx;
        float v = uv.x * uv.y * 15.0;
        return pow(v, 0.125);
    }

    void addImageLogo(inout vec4 o, vec2 p, sampler2D logo,
      float alpha, vec2 offset, mat2 lsmat) {
        vec2 ls = vec2(textureSize(logo, 0));
        vec2 bl = offset + ls*lsmat;
        vec2 p2 = p - bl;

        if (p2.x <= ls.x && p2.y <= ls.y && p2.x >= 0. && p2.y >= 0.) {
            vec4 col = texelFetch(logo, ivec2(p2), 0);
            float gray = (col.r + col.g + col.b) / 3.0;
            //o.rgb = mix(o.rgb, vec3(gray), col.a * alpha);
            vec3 neg = mix(o.rgb, 1.0 - o.rgb, gray);
            o.rgb = mix(o.rgb, neg, col.a * alpha);
        }
    }

    void main() {
      vec2 wh = vec2(textureSize(tDiffuse, 0));
      vec2 p = vUv * wh;
      vec4 o = texture(tDiffuse, vUv);
      addImageLogo(o, p, tImageLogo, 0.2, vec2(0), mat2(0.5));
      addImageLogo(o, p, tSignature, 0.8, vec2(wh.x, 0), mat2(-1, -1, 0, 1));
      o *= vignette();
      gl_FragColor = vec4(o.rgb, 1);
    }
  </script>

  <script type="importmap">
    {
      "imports": {
        "three": "/lib/three/three.module.js",
        "three/addons/": "/lib/three/examples/jsm/",
        "@sparkjsdev/spark": "/lib/spark.module.js",
        "@codemirror/view": "/lib/codemirror/@codemirror_view.js",
        "@codemirror/language": "/lib/codemirror/chunk-MED5PSS5.js",
        "@lezer/highlight": "/lib/codemirror/chunk-MED5PSS5.js"
      }
    }
  </script>

  <script type="module" src="3dgs.js"></script>
</body>

</html>